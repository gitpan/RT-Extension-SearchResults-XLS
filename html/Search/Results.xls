%# BEGIN BPS TAGGED BLOCK {{{
%#
%# COPYRIGHT:
%#
%# This software is Copyright (c) 1996-2014 Best Practical Solutions, LLC
%#                                          <sales@bestpractical.com>
%#
%# (Except where explicitly superseded by other copyright notices)
%#
%#
%# LICENSE:
%#
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%#
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%#
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%#
%#
%# CONTRIBUTION SUBMISSION POLICY:
%#
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%#
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%#
%# END BPS TAGGED BLOCK }}}
<%ARGS>
$Format => undef
$Query => ''
$OrderBy => 'id'
$Order => 'ASC'
$PreserveNewLines => 0
</%ARGS>
<%ONCE>
my $no_html = HTML::Scrubber->new( deny => '*' );
</%ONCE>
<%INIT>
require HTML::Entities;

use Spreadsheet::WriteExcel;
my $xls;
my $fh;
open ($fh, ">",  \$xls) or die "$!";
my $workbook = Spreadsheet::WriteExcel->new($fh) or die $!;
my $worksheet = $workbook->add_worksheet();


$r->content_type('application/vnd.ms-excel');

my $DisplayFormat = $m->comp('/Elements/ScrubHTML', Content => $Format);

my @Format = $m->comp('/Elements/CollectionAsTable/ParseFormat', Format => $DisplayFormat);

my @columns;

my $should_loc = { map { $_ => 1 } qw(Status) };

my $col_entry = sub {
    my $col = shift;
    # in tsv output, "#" is often a comment character but we use it for "id"
    delete $col->{title}
        if $col->{title} and $col->{title} =~ /^\s*#\s*$/;
    return {
        header => loc($col->{title} || $col->{attribute}),
        map    => $m->comp(
            "/Elements/ColumnMap",
            Name  => $col->{attribute},
            Attr  => 'value'
        ),
        should_loc => $should_loc->{$col->{attribute}},
    }
};

if ($PreserveNewLines) {
    my $col = [];
    push @columns, $col;
    for (@Format) {
        if ($_->{title} eq 'NEWLINE') {
            $col = [];
            push @columns, $col;
        }
        else {
            push @$col, $col_entry->($_);
        }
    }
}
else {
    push @columns, [map { $_->{attribute}
                          ? $col_entry->($_)
                          : () } @Format];
}

my $ws_col = 0;
for (@columns) {
    foreach my $ws_val (map { $_->{header} } @$_) {
        $worksheet->write_string(0, $ws_col, $ws_val);
        $ws_col++;
    }
}

my $Tickets = RT::Tickets->new( $session{'CurrentUser'} );
$Tickets->FromSQL( $Query );
if ( $OrderBy =~ /\|/ ) {
    # Multiple Sorts
    my @OrderBy = split /\|/, $OrderBy;
    my @Order   = split /\|/, $Order;
    $Tickets->OrderByCols(
        map { { FIELD => $OrderBy[$_], ORDER => $Order[$_] } }
        ( 0 .. $#OrderBy )
    );
}
else {
    $Tickets->OrderBy( FIELD => $OrderBy, ORDER => $Order );
}

my $ws_row = 1;
my $ii = 0;
while (my $row = $Tickets->Next) {
    $ws_col = 0;
    for my $col (@columns) {
        for (@$col) {
            my $val = ProcessColumnMapValue($_->{map}, Arguments => [$row, $ii++], Escape => 0);
            $val = loc($val) if $_->{should_loc};
            $val = '' unless defined $val;
            $val = $no_html->scrub($val);
            $val = HTML::Entities::decode_entities($val);
            $worksheet->write_string($ws_row, $ws_col, $val);
            $ws_col++;
        }
    }
    $ws_row++;
}

$workbook->close;
close($fh);
$m->print($xls);
$m->abort();

</%INIT>
